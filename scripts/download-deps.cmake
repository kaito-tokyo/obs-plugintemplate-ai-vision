cmake_minimum_required(VERSION 3.28)

if(NOT DEFINED WITH_OBS_STUDIO)
  set(WITH_OBS_STUDIO ON)
endif()

set(BUILDSPEC_FILE "${CMAKE_CURRENT_SOURCE_DIR}/buildspec.json")
set(DEPS_DIR "${CMAKE_CURRENT_SOURCE_DIR}/.deps")

macro(get_dependency_info BUILDSPEC_CONTENT DEP_NAME PLATFORM_KEY HAS_DEBUG_SYMBOLS)
  string(JSON DEP_VERSION GET "${BUILDSPEC_CONTENT}" dependencies "${DEP_NAME}" version)
  string(JSON DEP_BASEURL GET "${BUILDSPEC_CONTENT}" dependencies "${DEP_NAME}" baseUrl)
  string(JSON DEP_HASH GET "${BUILDSPEC_CONTENT}" dependencies "${DEP_NAME}" hashes "${PLATFORM_KEY}")
  if(HAS_DEBUG_SYMBOLS)
    string(
      JSON
      DEP_DEBUG_SYMBOLS_HASH
      GET "${BUILDSPEC_CONTENT}"
      dependencies
      "${DEP_NAME}"
      debugSymbols
      "${PLATFORM_KEY}"
    )
  endif()
endmacro()

function(download_and_check URL FILE EXPECTED_HASH)
  if(EXISTS "${FILE}")
    file(SHA256 "${FILE}" CURRENT_HASH)
    if(CURRENT_HASH STREQUAL EXPECTED_HASH)
      message(STATUS "Skipping download: ${FILE} (Hash matches)")
      return()
    endif()
  endif()

  message(STATUS "Downloading: ${URL}")
  file(DOWNLOAD "${URL}" "${FILE}" EXPECTED_HASH "SHA256=${EXPECTED_HASH}" STATUS DOWNLOAD_STATUS)

  list(GET DOWNLOAD_STATUS 0 STATUS_CODE)
  if(NOT STATUS_CODE EQUAL 0)
    list(GET DOWNLOAD_STATUS 1 STATUS_MSG)
    message(FATAL_ERROR "Download failed: ${STATUS_MSG}")
  endif()
endfunction()

if(NOT EXISTS "${BUILDSPEC_FILE}")
  message(FATAL_ERROR "buildspec.json not found at ${BUILDSPEC_FILE}")
endif()
file(READ "${BUILDSPEC_FILE}" BUILDSPEC_CONTENT)

file(MAKE_DIRECTORY "${DEPS_DIR}")

if(WIN32)
  # 1. OBS Studio
  if(WITH_OBS_STUDIO)
    get_dependency_info("${BUILDSPEC_CONTENT}" obs-studio windows-x64 OFF)
    set(FILENAME "obs-studio-${DEP_VERSION}.zip")
    set(EXTRACT_DIR "${DEPS_DIR}")

    download_and_check("${DEP_BASEURL}/${DEP_VERSION}.zip" "${DEPS_DIR}/${FILENAME}" "${DEP_HASH}")
    file(ARCHIVE_EXTRACT INPUT "${DEPS_DIR}/${FILENAME}" DESTINATION "${EXTRACT_DIR}")
  else()
    message(STATUS "Skipping obs-studio completely")
  endif()

  # 2. Prebuilt Deps
  get_dependency_info("${BUILDSPEC_CONTENT}" prebuilt windows-x64 OFF)
  set(FILENAME "windows-deps-${DEP_VERSION}-x64.zip")
  set(EXTRACT_DIR "${DEPS_DIR}/obs-deps-${DEP_VERSION}-x64")

  download_and_check("${DEP_BASEURL}/${DEP_VERSION}/${FILENAME}" "${DEPS_DIR}/${FILENAME}" "${DEP_HASH}")
  file(MAKE_DIRECTORY "${EXTRACT_DIR}")
  file(ARCHIVE_EXTRACT INPUT "${DEPS_DIR}/${FILENAME}" DESTINATION "${EXTRACT_DIR}")

  # 3. Qt6
  get_dependency_info("${BUILDSPEC_CONTENT}" qt6 windows-x64 ON)
  set(FILENAME "windows-deps-qt6-${DEP_VERSION}-x64.zip")
  set(EXTRACT_DIR "${DEPS_DIR}/obs-deps-qt6-${DEP_VERSION}-x64")

  download_and_check("${DEP_BASEURL}/${DEP_VERSION}/${FILENAME}" "${DEPS_DIR}/${FILENAME}" "${DEP_HASH}")
  file(MAKE_DIRECTORY "${EXTRACT_DIR}")
  file(ARCHIVE_EXTRACT INPUT "${DEPS_DIR}/${FILENAME}" DESTINATION "${EXTRACT_DIR}")
elseif(APPLE)
  set(VERSIONS_FILE "${DEPS_DIR}/.deps_versions")
  file(WRITE "${VERSIONS_FILE}" "# Auto-generated by download_deps.cmake\n")

  # 1. OBS Studio
  if(WITH_OBS_STUDIO)
    get_dependency_info("${BUILDSPEC_CONTENT}" obs-studio macos OFF)
    set(FILENAME "${DEP_VERSION}.tar.gz")
    set(EXTRACT_DIR "${DEPS_DIR}")

    file(APPEND "${VERSIONS_FILE}" "OBS_VERSION=\"${DEP_VERSION}\"\n")
    download_and_check("${DEP_BASEURL}/${FILENAME}" "${DEPS_DIR}/${FILENAME}" "${DEP_HASH}")
    file(ARCHIVE_EXTRACT INPUT "${DEPS_DIR}/${FILENAME}" DESTINATION "${EXTRACT_DIR}")
  else()
    message(STATUS "Skipping obs-studio completely")
  endif()

  # 2. Prebuilt Deps
  get_dependency_info("${BUILDSPEC_CONTENT}" prebuilt macos OFF)
  set(FILENAME "macos-deps-${DEP_VERSION}-universal.tar.xz")
  set(EXTRACT_DIR "${DEPS_DIR}/obs-deps-${DEP_VERSION}-universal")

  file(APPEND "${VERSIONS_FILE}" "PREBUILT_VERSION=\"${DEP_VERSION}\"\n")
  download_and_check("${DEP_BASEURL}/${DEP_VERSION}/${FILENAME}" "${DEPS_DIR}/${FILENAME}" "${DEP_HASH}")
  file(MAKE_DIRECTORY "${EXTRACT_DIR}")
  file(ARCHIVE_EXTRACT INPUT "${DEPS_DIR}/${FILENAME}" DESTINATION "${EXTRACT_DIR}")

  # 3. Qt6
  get_dependency_info("${BUILDSPEC_CONTENT}" qt6 macos ON)
  set(FILENAME "macos-deps-qt6-${DEP_VERSION}-universal.tar.xz")
  set(EXTRACT_DIR "${DEPS_DIR}/obs-deps-qt6-${DEP_VERSION}-universal")

  file(APPEND "${VERSIONS_FILE}" "QT6_VERSION=\"${DEP_VERSION}\"\n")
  download_and_check("${DEP_BASEURL}/${DEP_VERSION}/${FILENAME}" "${DEPS_DIR}/${FILENAME}" "${DEP_HASH}")
  file(MAKE_DIRECTORY "${EXTRACT_DIR}")
  file(ARCHIVE_EXTRACT INPUT "${DEPS_DIR}/${FILENAME}" DESTINATION "${EXTRACT_DIR}")

  execute_process(
    COMMAND xattr -r -d com.apple.quarantine "${DEPS_DIR}"
    RESULT_VARIABLE XATTR_RESULT
    OUTPUT_QUIET
    ERROR_QUIET
  )
else()
  message(FATAL_ERROR "This script must not be run on this platform.")
endif()

message(STATUS "All dependencies downloaded and extracted to ${DEPS_DIR}")
